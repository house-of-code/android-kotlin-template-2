apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-parcelize"
apply plugin: "kotlin-kapt"
apply plugin: "kotlinx-serialization"

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "io.houseofcode.template2"

        minSdkVersion 21
        targetSdkVersion 30

        versionCode 5
        versionName "2.2.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                    "room.schemaLocation": "$projectDir/schemas".toString(),
                    "room.incremental": "true",
                    "room.expandProjection": "true"
                ]
            }
        }
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties["keyAlias"]
            keyPassword keystoreProperties["keyPassword"]
            storeFile file(keystoreProperties["storeFile"])
            storePassword keystoreProperties["storePassword"]
        }
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
        }
        debug {
            versionNameSuffix "-debug"
            applicationIdSuffix ".debug"
        }
    }

    buildFeatures {
        // Enabled type safe views from generated binding of layouts into activities/fragments.
        // https://developer.android.com/topic/libraries/view-binding
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            output.outputFileName = "android-kotlin-template-2-$versionName-r$versionCode-${variant.buildType.name}.apk"
        }
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation project(":domain")
    implementation project(":data")

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.0.1"

    // Android
    implementation "androidx.appcompat:appcompat:1.2.0"
    implementation "com.google.android.material:material:1.3.0"
    implementation "androidx.core:core-ktx:1.3.2"
    implementation "androidx.activity:activity-ktx:1.2.0"
    implementation "androidx.fragment:fragment-ktx:1.3.0"
    implementation "androidx.constraintlayout:constraintlayout:2.0.4"
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation "androidx.activity:activity-ktx:1.3.0-alpha02"
    implementation "androidx.fragment:fragment-ktx:1.3.0"
    implementation "androidx.exifinterface:exifinterface:1.3.2"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.1"

    // LiveData / ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.0"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.3.0"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.3.0"
    implementation "androidx.lifecycle:lifecycle-process:2.3.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:2.3.0"

    // Retrofit / OkHttp
    implementation "com.squareup.okhttp3:logging-interceptor:4.3.1"
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:0.8.0"

    // Room.
    implementation "androidx.room:room-runtime:2.2.6"
    implementation "androidx.room:room-ktx:2.2.6"
    kapt "androidx.room:room-compiler:2.2.6"

    // Flipper debugging/diagnostic tool.
    debugImplementation "com.facebook.flipper:flipper:0.35.0"
    debugImplementation "com.facebook.flipper:flipper-network-plugin:0.35.0"
    debugImplementation "com.facebook.soloader:soloader:0.8.2"
    releaseImplementation "com.facebook.flipper:flipper-noop:0.35.0"

    // Glide
    implementation "com.github.bumptech.glide:glide:4.11.0"
    kapt "com.github.bumptech.glide:compiler:4.11.0"

    // Debugging.
    implementation "com.jakewharton.timber:timber:4.7.1"

    // Test
    testImplementation "androidx.test.ext:junit:1.1.2"
    androidTestImplementation "androidx.test:runner:1.3.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.3.0"
}
